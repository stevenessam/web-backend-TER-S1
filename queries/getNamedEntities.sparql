prefix fabio:   <http://purl.org/spar/fabio/>
prefix frbr:    <http://purl.org/vocab/frbr/core#>
PREFIX dct:    <http://purl.org/dc/terms/>
prefix oa:      <http://www.w3.org/ns/oa#>
prefix rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
prefix skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <http://schema.org/>

  SELECT DISTINCT ?entityText ?startPos ?endPos ?entityUri ?entityLabel
  FROM <http://ns.inria.fr/d2kab/graph/wheatgenomicsslkg>
  WHERE {
      <{id}> a fabio:ResearchPaper;
        dct:abstract ?abstract.
      ?subject frbr:partOf ?abstract.
    
    [] a oa:Annotation;
        oa:hasTarget [ oa:hasSource ?subject ;
                      oa:hasSelector [ oa:exact ?entityText;
                                        oa:start ?startPos;
                                        oa:end ?endPos ]] ;
        oa:hasBody ?entityUri.

      OPTIONAL {
          ?entityUri rdfs:label|skos:prefLabel|dct:title|schema:url ?entityLabel.
      }
  }
